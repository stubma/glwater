#ifdef GL_ES
precision mediump float;
#endif

varying vec3 vPosition;

uniform sampler2D causticTex;
uniform sampler2D water;
uniform vec3 sphereCenter;
uniform float sphereRadius;
uniform vec3 light;

const vec3 underwaterColor = vec3(0.4, 0.9, 1.0);
const float IOR_AIR = 1.0;
const float IOR_WATER = 1.333;

vec3 getSphereColor(vec3 point) {
    vec3 color = vec3(0.5);
    
    /* ambient occlusion with walls */
    color *= 1.0 - 0.9 / pow((1.0 + sphereRadius - abs(point.x)) / sphereRadius, 3.0);
    color *= 1.0 - 0.9 / pow((1.0 + sphereRadius - abs(point.z)) / sphereRadius, 3.0);
    color *= 1.0 - 0.9 / pow((point.y + 1.0 + sphereRadius) / sphereRadius, 3.0);
    
    /* caustics */
    vec3 sphereNormal = (point - sphereCenter) / sphereRadius;
    vec3 refractedLight = refract(-light, vec3(0.0, 1.0, 0.0), IOR_AIR / IOR_WATER);
    float diffuse = max(0.0, dot(-refractedLight, sphereNormal)) * 0.5;
    vec4 info = texture2D(water, point.xz * 0.5 + 0.5);
    if (point.y < info.r) {
        vec4 caustic = texture2D(causticTex, 0.75 * (point.xz - point.y * refractedLight.xz / refractedLight.y) * 0.5 + 0.5);
        diffuse *= caustic.r * 4.0;
    }
    color += diffuse;
    
    return color;
}

void main() {
    gl_FragColor = vec4(getSphereColor(vPosition), 1.0);
    vec4 info = texture2D(water, vPosition.xz * 0.5 + 0.5);
    if (vPosition.y < info.r) {
        gl_FragColor.rgb *= underwaterColor * 1.2;
    }
}